plugins {
    id "org.jetbrains.kotlin.jvm" version "1.5.21"
    id "org.jetbrains.intellij" version "1.1.4"
    id "org.ajoberstar.reckon" version "0.13.0"
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

if (!hasProperty('StudioCompilePath')) {
    throw new Exception("No StudioCompilePath value was set, please create gradle.properties file")
}

intellij {
    pluginName 'adb_idea_custom'
    updateSinceUntilBuild = false
    localPath = project.hasProperty("StudioRunPath") ? StudioRunPath : StudioCompilePath
    instrumentCode {
        compilerVersion = "212.4746.92"
    }
    instrumentTestCode {
        compilerVersion = "212.4746.92"
    }

}

group 'com.developerphil.intellij.plugin.adbidea'

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.5.21'
    implementation 'org.jooq:joor-java-8:0.9.14'
    compileOnly fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    compileOnly fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])

    testImplementation 'junit:junit:4.13.2'
    testImplementation fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    testImplementation fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])
    testImplementation "org.mockito:mockito-core:1.+"
    testImplementation 'com.google.truth:truth:1.1.3'
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

task(verifySetup) {
    doLast {
        def ideaJar = "$StudioCompilePath/lib/idea.jar"
        if (!file(ideaJar).exists()) {
            throw new Exception("$ideaJar not found, set StudioCompilePath in gradle.properties")
        }
    }
}

compileJava.dependsOn verifySetup
buildSearchableOptions.enabled = false